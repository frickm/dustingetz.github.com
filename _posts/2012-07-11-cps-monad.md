---
layout: post
title: demo of nonblocking list comprehension with cps monad
---

# {{ page.title }}

Just for fun. I'll explain how it works in another post. demo context is a music recommendation engine based off a social network graph containing info about users and their favorite tracks.

```clojure
    (ns cps-demo
      (:require [clojure.algo.monads :as m]))
```

fake an asynchronous query by sleeping in another thread then invoking the callback, passing through the argument `retval` untouched.

```clojure
    (defn delayed-async [retval callback]
      (future (Thread/sleep 1000)
              (callback retval)))           ; technically multithreaded but we could
                                            ; dispatch on main thread with some plumbing.
```

two mock queries, one that returns a user's favorite track, and one that returns users that favorited a track.

```clojure
    (def async-get-fav (partial delayed-async :track1))
    (def async-get-favr (partial delayed-async :user1))
```

here's the magic: non-blocking business logic. `m/cont-m` provides syntactic sugar over the callback plumbing so we can chain together ("compose") asynchronous (callback-taking) functions.

```clojure
    (defn recs-4 [callback]
      (m/domonad (m/maybe-t m/cont-m)
                 [fav (mk-cps async-get-fav)
                  favr (mk-cps async-get-favr)]
                 (callback fav favr)))

    ;; (m/run-cont (recs-4 prn))
```

this returns right away, then after 3 seconds prints the result to the repl. this is cool, but its just syntactic sugar over callbacks, nothing mind blowing yet.

now lets do it again, but the mock queries return lists.

```clojure
    (def async-get-favs (partial delayed-async [:track1 :track2 :track3]))
    (def async-get-favrs (partial delayed-async [:user1 :user2 :user3]))
```

business logic is a list comprehension now.

{% highlight ruby %}
    (defn recs-5 [callback]
      (m/domonad (m/sequence-t (m/maybe-t m/cont-m))
                 [fav (mk-cps async-get-favs)
                  favr (mk-cps async-get-favrs)]
                 (callback fav favr)))

    ;; (m/run-cont (recs-5 prn))
{% endhighlight %}

holy moley, a non-blocking list comprehension! it returns right away, and then as each callback is ready, it jumps back into the list comprehension ("for loops") in the right place and keeps going. craaazzyy! this would be difficult to write correctly without the monad stack. you could do it, but it would be nasty, and error-prone, lots of bugs. compare to the the monad comprehension which is obviously correct - we write the business logic the way it looks in our head, no incidental complexity of callbacks inside of loops.

here's the plumbing that makes it work:

{# highlight clojure %}
(m/with-monad m/cont-m
  (def mk-cont m-result))

(defn mk-cps
  "wrap a function `f` taking only a callback, suitable for use inside (maybe-t cont-m)
   we use maybe-monad to bail out of the cps monad comprehension without blocking, and
   when we have the async response we 'jump back into' the monad comprehension"
  [f]
  (m/call-cc
   (fn [c] ;`c` is a "callback" to the business logic in the monad comprehension
     (let [callback (fn [result]
                      (m/run-cont (c result)))]
       (f callback)
       (mk-cont nil)))))
{% endhighlight %}

series of blog posts explaining why this works to come.
